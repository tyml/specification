/* http://www.bottlecaps.de/rr/ui */

Document         ::= DocumentHeader WS? (AliasDef WS?)* Object WS?

SingleLineWS ::= ' ' | #x09
SimpleWS ::= SingleLineWS | #x0D | #x0A
WS       ::= (SimpleWS | '{--' Identifier? SimpleWS (AnyCharacter* SimpleWS)? Identifier? '--}')+

Identifier      ::= [a-zA-Z_][a-zA-Z0-9._-]*
NSIdentifier    ::= (Identifier '/')? Identifier

AnyCharacter            ::= /* Any character. */
OrdinaryStringCharacter ::= /* Any character, except '\', \n, \r, '<', '>' */
MarkupStringCharacter ::= /* Any character, except '\', '<', '>', '{', '}', '[', ']' */

EscapeSequence  ::= '\r' | '\n' | '\t' | '\\' | '\<' | '\>'
        | '\[' | '\]' | '\{' | '\}' | '\u' [0-9A-F][0-9A-F][0-9A-F][0-9A-F] | '\' #x0D? #x0A SingleLineWS*

DocumentHeader  ::= '{!tyml' (' 1.0')?
        (WS NSDefAttribute)* WS?
    '}'

AliasDef ::= '{!alias' WS  '<#' Identifier '>' WS OrdinaryString WS?  '}'

Element         ::= OrdinaryString | EscapedString | Primitive | Object | OrdinaryArray | MarkupArray

OrdinaryString  ::= '<' (OrdinaryStringCharacter | EscapeSequence)* '>'
EscapedString   ::= ('<<' (AnyCharacter* | '\' EscapeSequence) '>>') 
                    | ('<' Identifier  '<' (AnyCharacter* | '\' Identifier EscapeSequence) '>' Identifier  '>')

Primitive       ::= [a-zA-Z0-9_-+*=|~!?.,;/\"'()^&@%$#]+

Object          ::= '{' (NSIdentifier | '#' Identifier) 
                    (WS NSDefAttribute)* (WS (Element | Attribute))* WS? '}'
NSDefAttribute  ::= '!ns' ('/' Identifier)? ':' WS? OrdinaryString
Attribute       ::= NSIdentifier ':' WS? Element

OrdinaryArray   ::= '[' WS? (Element (WS Element)*)? WS? ']'

MarkupArray     ::= '![' MarkupElement* ']'
MarkupElement   ::= OrdinaryString | EscapedString | Object | MarkupString
MarkupString    ::= (MarkupStringCharacter | EscapeSequence)*
